plugins {
    id 'com.gradleup.shadow'
    id 'java'
    id 'com.adarshr.test-logger' version '4.0.0'
    id 'checkstyle'
    id 'org.gradlex.extra-java-module-info' version '1.13.1'
}

java {
    // Must be disabled in order to use the gradle-modules-plugin
    modularity.inferModulePath = false
}

def buildProperties = new Properties()
file('../core/src/main/resources/build.properties').withInputStream { buildProperties.load(it) }
group = 'info.openrocket'
version = buildProperties['build.version']

repositories {
    mavenCentral()
    maven { url = "https://jitpack.io" }
    maven { url = "https://jogamp.org/deployment/maven/" }
}

checkstyle {
    ignoreFailures = false
    maxWarnings = 0
}

configurations {
    testArtifactsClasspath {
        canBeConsumed = false
        canBeResolved = true
        visible = false
    }
}

// Some older libraries are not modularized, so we need to add module-info.java files for them.
extraJavaModuleInfo {
    failOnMissingModuleInfo.set(false)
    skipLocalJars.set(true)
    automaticModule('slf4j-api-1.7.25.jar', 'org.slf4j.api')
    automaticModule('slf4j-api-1.7.32.jar', 'org.slf4j.api')
    automaticModule('versioncompare-1.5.0.jar', 'versioncompare')
    module('gluegen-rt-2.5.0.jar', 'org.jogamp.gluegen.rt', '2.5.0') {
        requires('java.desktop')
        exportAllPackages()
    }
    module('jogl-all-2.5.0.jar', 'org.jogamp.jogl.all', '2.5.0') {
        requires('java.desktop')
        requires('org.jogamp.gluegen.rt')
        exportAllPackages()
    }
    module('jcommon-1.0.24.jar', 'jcommon', '1.0.24') {
        requires('java.desktop')
        exportAllPackages()
    }
    module('itextpdf-5.5.13.3.jar', 'itextpdf') {
        requires('java.desktop')
        exportAllPackages()
    }

    automaticModule('obj-0.4.0.jar', 'de.javagl.obj')
}

dependencies {
    implementation(project(path: ':core', configuration: 'default'))

    implementation 'de.javagl:obj:0.4.0'

    implementation 'org.slf4j:slf4j-api:2.0.12'
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation 'org.commonmark:commonmark:0.21.0'
    implementation 'com.google.inject:guice:7.0.0'
    implementation 'com.itextpdf:itextpdf:5.5.13.3'
    implementation 'org.jfree:jcommon:1.0.24'
    implementation 'org.jfree:jfreechart:1.5.4'
    implementation 'com.miglayout:miglayout-core:11.0'
    implementation 'com.miglayout:miglayout-swing:11.0'
    implementation 'com.fifesoft:rsyntaxtextarea:3.4.0'

    implementation 'io.github.g00fy2:versioncompare:1.5.0'
    implementation 'com.github.Dansoftowner:jSystemThemeDetector:3.8'

    implementation 'com.formdev:flatlaf:3.6.1'
    implementation 'com.formdev:flatlaf-extras:3.6.1'
    implementation 'com.formdev:flatlaf-intellij-themes:3.6.1'

    implementation 'ch.qos.logback:logback-core:1.5.0'
    implementation 'ch.qos.logback:logback-classic:1.5.0'

    implementation 'org.jogamp.gluegen:gluegen-rt-main:2.5.0'
    implementation 'org.jogamp.jogl:jogl-all-main:2.5.0'

    testImplementation 'ch.qos.logback:logback-classic:1.5.0'
}

// Ensure core jar is built before shadowJar resolves dependencies
// This prevents the extraJavaModuleInfo plugin from trying to transform a non-existent jar
tasks.named('shadowJar') {
    dependsOn(':core:jar')
}

jacocoTestCoverageVerification {
    dependsOn jacocoTestReport
    violationRules {
        rule {
            limit {
                minimum = 0.0
            }
        }
    }
}
